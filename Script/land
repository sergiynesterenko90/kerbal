CLEARSCREEN.

PRINT "boostback turnaround".
AG1 ON.
RCS ON.

SET TARGET_VECTOR TO HEADING(270,0).
LOCK STEERING TO R(0,0,90) + TARGET_VECTOR.

LOCK ANGLE TO VANG(TARGET_VECTOR:FOREVECTOR, SHIP:FACING:FOREVECTOR).
LOCK ROTATION_SPEED TO SHIP:ANGULARVEL:MAG.

LOCK THROTTLE to 0.0.

UNTIL ANGLE < 45 {
    WAIT 0.1.
}


print "precise alignment".
LOCK THROTTLE to 0.3.
UNTIL ANGLE < 5 AND ROTATION_SPEED < 0.05{
    PRINT ROTATION_SPEED.
    WAIT 0.1.
}



PRINT "boostback burn".

LOCK THROTTLE to 1.0.

UNTIL SHIP:GROUNDSPEED < 10 {
    PRINT SHIP:GROUNDSPEED.
    WAIT 0.1.
}


PRINT "reversed direction".

LOCK THROTTLE to 1.0.

UNTIL SHIP:GROUNDSPEED > 23 {
    PRINT SHIP:GROUNDSPEED.
    WAIT 0.1.
}




PRINT "coasting to apogee".
BRAKES ON.


SET thrott TO 0.0.
SET dthrott TO 0.
LOCK THROTTLE TO thrott.

SET g TO KERBIN:MU / KERBIN:RADIUS^2.
LOCK accvec TO SHIP:SENSORS:ACC - SHIP:SENSORS:GRAV.
LOCK gforce TO accvec:MAG / g.


//LOCK STEERING TO SHIP:UP.
SET TARGET_VECTOR TO HEADING(0,90).

UNTIL SHIP:VERTICALSPEED < 5 {
    WAIT 0.1.
}

PRINT "apogee".
WAIT 5.

PRINT "falling to 2km".

UNTIL ALT:RADAR < 1930.0 {
    WAIT 0.1.
}

PRINT "deceleration burn - stage 1 - 3.3g".
LOCK dthrott TO 0.05 * (3.3 - gforce).

UNTIL SHIP:VELOCITY:SURFACE:MAG < 50 {
    SET thrott to thrott + dthrott.
    WAIT 0.1.
}


PRINT "deceleration burn - stage 2 - 1.7g".
LOCK dthrott TO 0.05 * (1.7 - gforce).

UNTIL SHIP:VELOCITY:SURFACE:MAG < 20 {
    SET thrott to thrott + dthrott.
    WAIT 0.1.
}


PRINT "deceleration burn - stage 3 - 1.1g".
LOCK dthrott TO 0.05 * (1.1 - gforce).

GEAR ON.

UNTIL SHIP:VELOCITY:SURFACE:MAG < 5 {
    SET thrott to thrott + dthrott.
    WAIT 0.1.

}


PRINT "landing burn - stage 4 - 1g".
LOCK dthrott TO 0.05 * (1.0 - gforce).

UNTIL ALT:RADAR < 34 {
    SET thrott to thrott + dthrott.
    PRINT ALT:RADAR.
    WAIT 0.1.
}

PRINT "touchdown".

SET thrott to 0.
SET dthrott to 0.

// retract air brakes +style
WAIT 10.
BRAKES OFF.
